
=================================================================
Описание БНФ-нотации
   (используется для задания КС-грамматики или СУ-схем)

1. Нетерминалы Vn изображаются цепочкой символов, заключенных в <>.
2. Терминалы Vt изображаются без изменений.
3. Правила записываются в виде Vn ::= (Vn+Vt)*.
4. Альтернативы записываются через cимвол |.


Описание РБНФ-нотации 
   (от Вирта для Паскаля)

1. Нетерминалы Vn изображаются без изменений как цепочки символов.
2. Терминалы Vt заключаются в апострофы.
3. Правила записываются в виде Vn = (Vn+Vt)*.
4. Альтернативы записываются через cимвол |.
5. Необязательные конструкции заключаются в [].
6. Итерация конструкции заключается в {}.
7. Конструкции в () употребляются в обычном смысле.

=================================================================

Описательная мощность БНФ и РБНФ одинакова:

1. [] в РБНФ
   
     А = [B]C | D
   ----------------
   A ::= BC | C | D

2. {} в РБНФ

     А = {B}C | D
   ----------------
   A ::= XC | C | D
   X ::= BX | B

=================================================================

Примеры:

а) РБНФ для РБНФ

РБНФ-описание		=	{РБНФ-определение}
РБНФ-определение	=	нетерминал '=' выражение
выражение		=	выражение '|' выражение	|
				'{' выражение '}'	|
				'[' выражение ']'	|
				'(' выражение ')'	|
				нетерминал		|
				терминал

нетерминал		=	строка
терминал		=	''' строка '''
строка			=	символ{символ}

б) БНФ для идентификатора

<идентификатор> ::=  	<буква> | <буква><продолжение>
<продолжение> 	::=  	<символ> | <символ><продолжение>
<символ>	::=  	<буква> | <цифра>
<буква> 	::=  	a | b | ... | z
<цифра>		::=	0 | 1 | ... | 9

в) РБНФ для идентификатора

идентификатор	= 	буква{(буква|цифра)}
буква		= 	'a' | 'b' | ... | 'z'
цифра		= 	'0' | '1' | ... | '9'

=================================================================

Дополнение РБНФ-нотации

1. Итерация {} может иметь верхний и нижний пределы (необязательные)
	а{expr}b <=> от а до b итераций expr

 Пример: 
   идентификатор = буква{(буква|цифра)}254
   (Длина идентификатора не должна превышать 255 символов)

2. Для удобства разделения нетерминалов вводится символ '+'. 

 Пример:
   телефон = цифра+цифра+цифра+'-'+цифра+цифра+'-'+цифра+цифра
   (в БНФ роль символа '+' исполняют скобки <>)   

=================================================================
